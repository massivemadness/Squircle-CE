/*
 * Copyright 2023 Squircle CE contributors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    namespace 'com.blacksquircle.ui'

    defaultConfig {
        applicationId "com.blacksquircle.ui"

        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk

        versionCode 10014
        versionName "2023.1.0"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    flavorDimensions "store"
    productFlavors {
        googlePlay { dimension "store" }
        fdroid { dimension "store" }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        benchmark {
            signingConfig signingConfigs.debug
            matchingFallbacks = ['release']
            proguardFiles 'benchmark-rules.pro'
            debuggable false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        fdroid.java.srcDirs += 'src/fdroid/kotlin'
        googlePlay.java.srcDirs += 'src/googlePlay/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {

    // Core
    implementation library.kotlin
    implementation library.core
    implementation library.activity
    implementation library.fragment
    implementation library.splash
    implementation library.profileinstaller
    implementation library.timber

    // Google Play
    googlePlayImplementation library.playcore

    // UI
    implementation library.appcompat
    implementation library.materialdesign
    implementation library.materialdialogs_core

    // AAC
    implementation library.livedata
    implementation library.viewmodel
    implementation library.lifecycle
    implementation library.navigation_ui
    implementation library.navigation_fragment

    // Coroutines
    implementation library.coroutines_core
    implementation library.coroutines_android

    // DI
    implementation library.hilt
    implementation library.hilt_worker
    kapt library.hilt_compiler

    // Modules
    implementation project(':feature-editor-api')
    implementation project(':feature-editor-impl')
    implementation project(':feature-explorer-api')
    implementation project(':feature-explorer-impl')
    implementation project(':feature-fonts-api')
    implementation project(':feature-fonts-impl')
    implementation project(':feature-settings-api')
    implementation project(':feature-settings-impl')
    implementation project(':feature-themes-api')
    implementation project(':feature-themes-impl')
    implementation project(':common-core')
    implementation project(':common-ui')

    implementation project(':filesystems:filesystem-base')
    implementation project(':filesystems:filesystem-local') // TODO remove

    // Tests
    testImplementation library.junit
    androidTestImplementation library.junit_ext
    androidTestImplementation library.test_runner
}
